(mod (
      OPERATIONS
      MOD_HASH
      (
        TREASURY_APPROVAL_MOD_HASHES ; puzzle mods that are allowed to withdraw/mint stable-coins or governance token (CRT)
        P2_TREASURY_MOD_HASH
        OFFER_MOD_HASH
        P2_SURPLUS_AUCTION_MOD_HASH
      )
      CAT_MOD_HASH ; TODO: move before MOD_HASH?
      CRT_TAIL_HASH
      STATUTES ; -> ((value proposal_threshold veto_period implementation_delay maximum_delta))
      PRICE_INFO ; Statute price info. -> (price . last_updated)
      ; cumulative stability fee discount factor as of last Statutes price info update
      PAST_CUMULATIVE_STABILITY_FEE_DF
      ; cumulative interest rate discount factor as of last Statutes price info update
      PAST_CUMULATIVE_INTEREST_DF
      ; counter of all price updates
      PRICE_UPDATE_COUNTER

      ; solution
      ; ***NEW*** needed to calculate puzhash of proposal coin when mutating statute, nil o/w
      ;   info not included in mutation is proposal periods, statutes struct, governance inner puzzle hash
      governance_curried_args_hash ; as required by tree_hash_of_apply
      mutation ; (operation mutation_index mutation_value) when mutating statute or updating price, nil when announcing
     )

  (include *standard-cl-23.1*)
  (include curry.clib)
  (include condition_codes.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (defun announce-statutes (statutes start_index rest)
    (if statutes
      (c
        (list CREATE_PUZZLE_ANNOUNCEMENT
          (concat PROTOCOL_PREFIX
            STATUTE_PREFIX start_index (f (f statutes))
          )
        )
        ; add a full statutes when announcing statutes
        (c
          (list CREATE_PUZZLE_ANNOUNCEMENT
            (concat PROTOCOL_PREFIX
              STATUTE_FULL_PREFIX start_index (sha256tree (f statutes))
            )
          )
          (announce-statutes (r statutes) (+ start_index ONE) rest)
        )
      )
      rest
    )
  )

  (defun-inline create-coin-condition (MOD_HASH
                                        TREASURY_APPROVAL_MOD_HASHES
                                        P2_TREASURY_MOD_HASH P2_SURPLUS_AUCTION_MOD_HASH
                                        CAT_MOD_HASH OFFER_MOD_HASH CRT_TAIL_HASH
                                        statutes price_info cumulative_stability_fee_df cumulative_interest_df price_update_counter)
    (list
      CREATE_COIN
      ; NOTE: we don't curry in operations here as they're already curried in mod hash
      (curry_hashes MOD_HASH
        (sha256 ONE MOD_HASH)
        (sha256tree
          (list
            TREASURY_APPROVAL_MOD_HASHES
            P2_TREASURY_MOD_HASH
            OFFER_MOD_HASH
            P2_SURPLUS_AUCTION_MOD_HASH
          )
        )
        (sha256 ONE CAT_MOD_HASH)
        (sha256 ONE CRT_TAIL_HASH)
        (sha256tree statutes)
        (sha256tree price_info)
        (sha256 ONE cumulative_stability_fee_df)
        (sha256 ONE cumulative_interest_df)
        (sha256 ONE price_update_counter)
      )
      ; amount, 1 mojo for singletons or any odd number
      ONE
    )
  )


  (assign
    (operation mutation_index mutation_value) (if mutation mutation (() () ()))
    (operation_mod . operation_mod_hash) (if operation (c operation (sha256tree operation)) (c () ()))
    (statutes price_info cumulative_stability_fee_df cumulative_interest_df price_update_counter mutation_conditions)
      (if operation_mod_hash
        ; mutate statutes or update price info
        (assert
          (any (= operation_mod_hash (f OPERATIONS)) (= operation_mod_hash (r OPERATIONS)))
          (a
            operation_mod
            (list
              MOD_HASH STATUTES PRICE_INFO PAST_CUMULATIVE_STABILITY_FEE_DF PAST_CUMULATIVE_INTEREST_DF
              CAT_MOD_HASH CRT_TAIL_HASH PRICE_UPDATE_COUNTER
              mutation_index mutation_value
              governance_curried_args_hash ; ***NEW***
            )
          )
        )
        ; announce. default to existing state. no operation conditions
        (list STATUTES PRICE_INFO PAST_CUMULATIVE_STABILITY_FEE_DF PAST_CUMULATIVE_INTEREST_DF PRICE_UPDATE_COUNTER ())
      )
    (li
      (create-coin-condition
        MOD_HASH
        TREASURY_APPROVAL_MOD_HASHES
        P2_TREASURY_MOD_HASH
        P2_SURPLUS_AUCTION_MOD_HASH
        CAT_MOD_HASH
        OFFER_MOD_HASH
        CRT_TAIL_HASH
        statutes
        price_info
        cumulative_stability_fee_df
        cumulative_interest_df
        price_update_counter
      )
      (list REMARK PROTOCOL_PREFIX statutes price_info cumulative_stability_fee_df cumulative_interest_df price_update_counter)
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX STATUTE_PREFIX -7 price_update_counter)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX STATUTE_PREFIX -6 P2_SURPLUS_AUCTION_MOD_HASH)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX STATUTE_PREFIX -5 OFFER_MOD_HASH)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX STATUTE_PREFIX -4 P2_TREASURY_MOD_HASH)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX STATUTE_PREFIX -3 TREASURY_APPROVAL_MOD_HASHES)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX STATUTE_PREFIX -2 cumulative_interest_df)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX STATUTE_PREFIX MINUS_ONE cumulative_stability_fee_df)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat PROTOCOL_PREFIX PRICE_PREFIX (sha256tree price_info))
      )
      &rest
      (announce-statutes statutes 0 mutation_conditions)
    )
  )
)
