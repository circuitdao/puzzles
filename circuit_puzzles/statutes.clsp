(mod (
      (
        MOD_HASH .
        OPERATIONS
      )
      (
        ; puzzle mods that are allowed to withdraw/mint stable-coins or governance token(CRT)
        TREASURY_APPROVAL_MOD_HASHES
        P2_TREASURY_MOD_HASH
        OFFER_MOD_HASH
        P2_SURPLUS_AUCTION_MOD_HASH
      )
      CAT_MOD_HASH
      CRT_TAIL_HASH
      ; statutes are (oracle_launcher_id . statutes) where ORACLE_PRICE_INFO are a list of
      STATUTES
      ; PRICE_INFO -> ( price . last_updated)
      PRICE_INFO
      ; cumulative fee updated since start, updated whenever oracle prices are updated
      PAST_CUMULATIVE_STABILITY_FEE_RATE
      ; cumulative interest rate fee, same schedule as stability fee
      PAST_CUMULATIVE_INTEREST_RATE
      ; counter of all price updates
      PRICE_UPDATES

      ; solution
      crt_inner_puzzle_hash 
      mutation
     )

  (include *standard-cl-23*)
  (include curry.clib)
  (include condition_codes.clib)
  (include statutes_utils.clib)
  (include utils.clib)


  (defun announce-statutes (statutes prefix start_index rest)
    (if statutes
      (c
        (list CREATE_PUZZLE_ANNOUNCEMENT
          (concat STATUTES_ANN_PREFIX
            (sha256tree
              (list prefix start_index (if (= prefix 's') (f (f statutes)) (f statutes)))
            )
          )
        )
        (if (= prefix 's')
          ; add a full statutes when announcing statutes
          (c
            (list CREATE_PUZZLE_ANNOUNCEMENT
              (concat STATUTES_ANN_PREFIX
                (sha256tree
                  (list (concat prefix 'f') start_index (f statutes))
                )
              )
            )
            (announce-statutes (r statutes) prefix (+ start_index ONE) rest)
          )
          (announce-statutes (r statutes) prefix (+ start_index ONE) rest)
        )
      )
      rest
    )
  )

  (defun-inline create-coin-condition ((MOD_HASH . OPERATIONS) TREASURY_APPROVAL_MOD_HASHES
                                        P2_TREASURY_MOD_HASH P2_SURPLUS_AUCTION_MOD_HASH
                                        CAT_MOD_HASH OFFER_MOD_HASH CRT_TAIL_HASH
                                        statutes price_info cumulative_stability_fee cumulative_interest_rate price_updates)
    (list
      CREATE_COIN
      ; puzzle
      (curry_hashes MOD_HASH
        (sha256tree (c MOD_HASH OPERATIONS))
        (sha256tree
          (list
            TREASURY_APPROVAL_MOD_HASHES
            P2_TREASURY_MOD_HASH
            OFFER_MOD_HASH
            P2_SURPLUS_AUCTION_MOD_HASH
          )
        )
        (sha256 ONE CAT_MOD_HASH)
        (sha256 ONE CRT_TAIL_HASH)
        (sha256tree statutes)
        (sha256tree price_info)
        (sha256 ONE cumulative_stability_fee)
        (sha256 ONE cumulative_interest_rate)
        (sha256 ONE price_updates)
      )
      ; amount, 1 mojo for singletons or any odd number
      ONE
    )
  )

  (defun filter-ann-conditions (
    (@ ann_conditions
      (
        (@ condition
          (condition_code . condition_body)
        )
        . rest
      )
    ) filtered_announcements)
    (if ann_conditions
      (if
        (any
            (= condition_code CREATE_PUZZLE_ANNOUNCEMENT) (= condition_code CREATE_COIN_ANNOUNCEMENT)
        )
        (if (= (substr (f condition_body) 0 ONE) STATUTES_ANN_PREFIX)
          ; bad announcement, error
          (x)
          (filter-ann-conditions rest (c condition filtered_announcements))
        )
        (if (any (= condition_code ASSERT_PUZZLE_ANNOUNCEMENT) (= condition_code ASSERT_COIN_ANNOUNCEMENT))
          (filter-ann-conditions rest (c condition filtered_announcements))
          (filter-ann-conditions rest filtered_announcements)
        )
      )
      filtered_announcements
    )
  )

  (assign
    (operation mutation_index mutation_value) (if mutation mutation (() () ()))
    (operation_mod . operation_mod_hash) (if operation (c operation (sha256tree operation)) (c () ()))
    (statutes price_info cumulative_stability_fee cumulative_interest_rate price_updates mutation_conditions)
      (if (= operation_mod_hash ())
        ; default to existing values
        (list STATUTES PRICE_INFO PAST_CUMULATIVE_STABILITY_FEE_RATE PAST_CUMULATIVE_INTEREST_RATE PRICE_UPDATES ())
        ; mutate statutes
        (assert_debug
          (any (= operation_mod_hash (f OPERATIONS)) (= operation_mod_hash (r OPERATIONS)))
          (> mutation_index -3)
          (> 3 (strlen mutation_index))
          (l mutation_value)
          (a
            operation_mod
            (list
              STATUTES PRICE_INFO PAST_CUMULATIVE_STABILITY_FEE_RATE PAST_CUMULATIVE_INTEREST_RATE CAT_MOD_HASH CRT_TAIL_HASH PRICE_UPDATES
              mutation_index mutation_value crt_inner_puzzle_hash
            )
          )
        )
      )
    create_coin_condition (create-coin-condition
      (c MOD_HASH OPERATIONS)
      TREASURY_APPROVAL_MOD_HASHES
      P2_TREASURY_MOD_HASH
      P2_SURPLUS_AUCTION_MOD_HASH
      CAT_MOD_HASH
      OFFER_MOD_HASH
      CRT_TAIL_HASH
      statutes
      price_info
      cumulative_stability_fee
      cumulative_interest_rate
      price_updates
    )
    ; add announcements if any
    announcements (if (all operation_mod_hash (= mutation_index -1))
      ; filter announcements
      (filter-ann-conditions (f mutation_value) mutation_conditions)
      ; no announcements
      mutation_conditions
    )
    (c create_coin_condition
      (c
        (list CREATE_PUZZLE_ANNOUNCEMENT
          (concat STATUTES_ANN_PREFIX (sha256tree (list STATUTE_PREFIX -7 PRICE_UPDATES)))
        )
        (c
          (list CREATE_PUZZLE_ANNOUNCEMENT
            (concat STATUTES_ANN_PREFIX (sha256tree (list STATUTE_PREFIX -6 P2_SURPLUS_AUCTION_MOD_HASH)))
          )
          (c
            (list CREATE_PUZZLE_ANNOUNCEMENT
              (concat STATUTES_ANN_PREFIX (sha256tree (list STATUTE_PREFIX -5 OFFER_MOD_HASH)))
            )
            (c
              (list CREATE_PUZZLE_ANNOUNCEMENT
                (concat STATUTES_ANN_PREFIX (sha256tree (list STATUTE_PREFIX -4 P2_TREASURY_MOD_HASH)))
              )
              (c
                (list CREATE_PUZZLE_ANNOUNCEMENT
                  (concat STATUTES_ANN_PREFIX (sha256tree (list STATUTE_PREFIX -3 TREASURY_APPROVAL_MOD_HASHES)))
                )
                (c
                  (list CREATE_PUZZLE_ANNOUNCEMENT
                    (concat STATUTES_ANN_PREFIX (sha256tree (list STATUTE_PREFIX -2 cumulative_interest_rate)))
                  )
                  (c
                    (list CREATE_PUZZLE_ANNOUNCEMENT
                      (concat STATUTES_ANN_PREFIX (sha256tree (list STATUTE_PREFIX -1 cumulative_stability_fee)))
                    )
                    (c
                      (list CREATE_PUZZLE_ANNOUNCEMENT
                        (concat STATUTES_ANN_PREFIX (sha256tree (list PRICE_PREFIX price_info)))
                      )
                      (announce-statutes statutes STATUTE_PREFIX 0 announcements)
                    )
                  )
                )
              )
            )
          )
        )
      )
    )
  )
)
