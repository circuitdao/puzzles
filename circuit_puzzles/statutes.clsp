(mod (
      OPERATIONS
      MOD_HASH
      (
        TREASURY_APPROVAL_MOD_HASHES ; puzzle mods that are allowed to withdraw/mint stable-coins or governance token(CRT)
        P2_TREASURY_MOD_HASH
        OFFER_MOD_HASH
        P2_SURPLUS_AUCTION_MOD_HASH
      )
      CAT_MOD_HASH
      CRT_TAIL_HASH
      ; statutes are (oracle_launcher_id . statutes) where ORACLE_PRICE_INFO are a list of
      STATUTES
      ; PRICE_INFO -> ( price . last_updated)
      PRICE_INFO
      ; cumulative fee updated since start, updated whenever oracle prices are updated
      PAST_CUMULATIVE_STABILITY_FEE_DF
      ; cumulative interest rate fee, same schedule as stability fee
      PAST_CUMULATIVE_INTEREST_DF
      ; counter of all price updates
      PRICE_UPDATE_COUNTER

      ; solution
      crt_inner_puzzle_hash 
      mutation
     )

  (include *standard-cl-23.1*)
  (include curry.clib)
  (include condition_codes.clib)
  (include statutes_utils.clib)
  (include utils.clib)


  (defun announce-statutes (statutes start_index rest)
    (if statutes
      (c
        (list CREATE_PUZZLE_ANNOUNCEMENT
          (concat STATUTES_ANN_PREFIX
            STATUTE_PREFIX start_index (f (f statutes))
          )
        )
        ; add a full statutes when announcing statutes
        (c
          (list CREATE_PUZZLE_ANNOUNCEMENT
            (concat STATUTES_ANN_PREFIX
              STATUTE_FULL_PREFIX start_index (sha256tree (f statutes))
            )
          )
          (announce-statutes (r statutes) (+ start_index ONE) rest)
        )
      )
      rest
    )
  )

  (defun-inline create-coin-condition (MOD_HASH TREASURY_APPROVAL_MOD_HASHES
                                        P2_TREASURY_MOD_HASH P2_SURPLUS_AUCTION_MOD_HASH
                                        CAT_MOD_HASH OFFER_MOD_HASH CRT_TAIL_HASH
                                        statutes price_info cumulative_stability_fee cumulative_interest_rate price_updates)
    (list
      CREATE_COIN
      ; NOTE: we don't curry in operations here as they're already curried in mod hash
      (curry_hashes MOD_HASH
        (sha256 ONE MOD_HASH)
        (sha256tree
          (list
            TREASURY_APPROVAL_MOD_HASHES
            P2_TREASURY_MOD_HASH
            OFFER_MOD_HASH
            P2_SURPLUS_AUCTION_MOD_HASH
          )
        )
        (sha256 ONE CAT_MOD_HASH)
        (sha256 ONE CRT_TAIL_HASH)
        (sha256tree statutes)
        (sha256tree price_info)
        (sha256 ONE cumulative_stability_fee)
        (sha256 ONE cumulative_interest_rate)
        (sha256 ONE price_updates)
      )
      ; amount, 1 mojo for singletons or any odd number
      ONE
    )
  )


  (assign
    (operation mutation_index mutation_value) (if mutation mutation (() () ()))
    (operation_mod . operation_mod_hash) (if operation (c operation (sha256tree operation)) (c () ()))
    (statutes price_info cumulative_stability_fee cumulative_interest_rate price_updates mutation_conditions)
      (if operation_mod_hash
        ; mutate statutes
        (assert
          (any (= operation_mod_hash (f OPERATIONS)) (= operation_mod_hash (r OPERATIONS)))
          (a
            operation_mod
            (list
              STATUTES PRICE_INFO PAST_CUMULATIVE_STABILITY_FEE_DF PAST_CUMULATIVE_INTEREST_DF CAT_MOD_HASH CRT_TAIL_HASH PRICE_UPDATE_COUNTER
              mutation_index mutation_value crt_inner_puzzle_hash
            )
          )
        )
        ; default to existing values
        (list STATUTES PRICE_INFO PAST_CUMULATIVE_STABILITY_FEE_DF PAST_CUMULATIVE_INTEREST_DF PRICE_UPDATE_COUNTER ())
      )
    (li
      (list REMARK statutes price_info cumulative_stability_fee cumulative_interest_rate price_updates)
      (create-coin-condition
        MOD_HASH
        TREASURY_APPROVAL_MOD_HASHES
        P2_TREASURY_MOD_HASH
        P2_SURPLUS_AUCTION_MOD_HASH
        CAT_MOD_HASH
        OFFER_MOD_HASH
        CRT_TAIL_HASH
        statutes
        price_info
        cumulative_stability_fee
        cumulative_interest_rate
        price_updates
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX STATUTE_PREFIX -7 price_updates)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX STATUTE_PREFIX -6 P2_SURPLUS_AUCTION_MOD_HASH)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX STATUTE_PREFIX -5 OFFER_MOD_HASH)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX STATUTE_PREFIX -4 P2_TREASURY_MOD_HASH)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX STATUTE_PREFIX -3 TREASURY_APPROVAL_MOD_HASHES)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX STATUTE_PREFIX -2 cumulative_interest_rate)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX STATUTE_PREFIX MINUS_ONE cumulative_stability_fee)
      )
      (list CREATE_PUZZLE_ANNOUNCEMENT
        (concat STATUTES_ANN_PREFIX PRICE_PREFIX (sha256tree price_info))
      )
      &rest
      (announce-statutes statutes 0 mutation_conditions)
    )
  )
)
