(mod (OPERATIONS BYC_TAIL_MOD_HASH
      MOD_HASH STATUTES_STRUCT LAUNCHER_ID AUCTION_PARAMS LAST_BID
      (
        lineage_proof
        operation
        input_conditions
        op_args ;. op_args
      )
    )

  (include *standard-cl-23.1*)
  (include prefixes.clib)
  (include utils.clib)
  (include condition_codes.clib)
  (include condition_filtering.clib)
  (include curry.clib)
  (include statutes_utils.clib)

  (assign
    byc_tail_hash (curry_hashes BYC_TAIL_MOD_HASH (sha256tree STATUTES_STRUCT))
    operation_hash (sha256tree operation)
    (assert
      (contains OPERATIONS operation_hash)
      (fail-on-protocol-condition-or-create-coin input_conditions)
      (c
        (list SEND_MESSAGE 0x38 ; coin ID to none
          (sha256tree (list lineage_proof operation_hash input_conditions op_args))
        ) ; secure solution
        (a
          operation
          (list
            (list MOD_HASH STATUTES_STRUCT byc_tail_hash LAUNCHER_ID AUCTION_PARAMS LAST_BID)
            lineage_proof input_conditions op_args
          )
        )
      )
    )
  )
)
