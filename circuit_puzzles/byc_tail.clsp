(mod (STATUTES_STRUCT
      Truths
      parent_is_cat
      lineage_proof
      delta
      inner_conditions
      (@ solution
        (
          vault_parent_id
          vault_mod_hash
          vault_curried_args_hash
          vault_amount
          statutes_inner_puzzle_hash
          approval_mod_hashes
          run_tail_mod_hash
          current_coin_amount
          new_byc_coin_amount ; new amount of BYC coin being issued or melted from
        )
      )
     )

  (include *standard-cl-23.1*)
  (include condition_codes.clib)
  (include cat_truths.clib)
  (include curry.clib)
  (include statutes_utils.clib)
  (include utils.clib)
  (include condition_filtering.clib)

  (if (any (= delta 0) (> 0 delta))
    ; we're issuing or melting
    (assign
      vault_coin_id (calculate-coin-id
        vault_parent_id
        (tree_hash_of_apply vault_mod_hash vault_curried_args_hash)
        vault_amount
      )
      CAT_MOD_HASH (cat_mod_hash_truth Truths)
      CAT_MOD_HASH_HASH (cat_mod_hash_hash_truth Truths)
      byc_tail_hash (cat_tail_program_hash_truth Truths)
      statutes_puzzle_hash (calculate-statutes-puzzle-hash STATUTES_STRUCT statutes_inner_puzzle_hash)
      (assert
        ; check for cheeky conditions
        (check-tail-conditions inner_conditions)
        (size_b32 vault_curried_args_hash)
        (= vault_mod_hash (f approval_mod_hashes))
        (any
          ; when melting, parent must be a cat, and delta must match negative new_byc_coin_amount
          (all parent_is_cat (= new_byc_coin_amount (+ current_coin_amount delta)))
          ; when issuing, parent must not be a cat, and delta must be 0
          (all (not parent_is_cat) (= 0 delta))
        )
        (list
          (list ASSERT_MY_AMOUNT current_coin_amount)
          ; this tail can only be called in run tail puzzle to avoid misuse
          (list ASSERT_MY_PUZZLE_HASH
            (curry_hashes CAT_MOD_HASH
              CAT_MOD_HASH_HASH
              (sha256tree byc_tail_hash)
              run_tail_mod_hash
            )
          )
          (list RECEIVE_MESSAGE 0x3f
            (concat
              PROTOCOL_PREFIX
              (sha256tree
                (c (if (> 0 delta) "x" "i")
                  (c (if (> 0 delta) delta new_byc_coin_amount)
                    STATUTES_STRUCT ; statutes_struct_hash ; need to tie it to statutes struct
                  )
                )
              )
            )
            vault_coin_id
          )
          (assert-statute statutes_puzzle_hash STATUTE_APPROVAL_MOD_HASHES_HASH (sha256tree approval_mod_hashes))
          (assert-statute statutes_puzzle_hash STATUTE_RUN_TAIL_MOD_HASH run_tail_mod_hash)
        )
      )
    )
    (x) ; no minting
  )
)
