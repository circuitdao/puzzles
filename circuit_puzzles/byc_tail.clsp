(mod (STATUTES_STRUCT
      Truths
      parent_is_cat
      lineage_proof
      delta
      inner_conditions ; we don't check these as since they're checked in vault puzzle,
                       ; which is the only one that can approve minting
      (@ solution
        (
          vault_parent_id
          vault_mod_hash
          vault_curried_args
          vault_amount
          cat_ann_prefix
          statutes_inner_puzzle_hash
          approved_mod_hashes
          current_coin_amount
          new_melting_coin_amount
          my_coin_id
        )
      )
     )

  (include *standard-cl-23*)
  (include cat_truths.clib)
  (include condition_codes.clib)
  (include curry.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (defconst ANN_MELT "x")
  (defconst ANN_ISSUANCE "i")

  (if (any (= delta 0) (> 0 delta))
    ; we're issuing or melting
    (assign
      vault_coin_id (calculate-coin-id
        vault_parent_id
        (tree_hash_of_apply vault_mod_hash vault_curried_args)
        vault_amount
      )
      statutes_struct_hash (sha256tree STATUTES_STRUCT)
      statutes_puzzle_hash (calculate-statutes-puzzle-hash STATUTES_STRUCT statutes_inner_puzzle_hash)
      (assert
        (size_b1 cat_ann_prefix)
        (size_b32 vault_curried_args)
        (= vault_mod_hash (f approved_mod_hashes))
        (any
          ; when melting, parent must be a cat, and delta must match negative new_melting_coin_amount
          (all parent_is_cat (= new_melting_coin_amount (+ current_coin_amount delta)))
          (all (not parent_is_cat) (= 0 delta))
        )
        (list
          (list ASSERT_MY_AMOUNT current_coin_amount)
          (list ASSERT_MY_COIN_ID my_coin_id)  ; thanks @bramv-chia
          (list
            RECEIVE_MESSAGE
            0x3f
            (concat
              cat_ann_prefix
              (if (> 0 delta) ANN_MELT ANN_ISSUANCE)
              my_coin_id
              (if (> 0 delta) delta new_melting_coin_amount)
              statutes_struct_hash ; need to tie it to statutes struct
            )
            vault_coin_id
          )
          (assert-statute statutes_puzzle_hash STATUTE_TREASURY_COIN_APPROVER_MOD_HASHES (sha256tree approved_mod_hashes))
        )
      )
    )
    (x)  ; no minting
  )
)
