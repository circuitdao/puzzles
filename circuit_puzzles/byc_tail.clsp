(mod (STATUTES_STRUCT
      Truths parent_is_cat lineage_proof delta inner_conditions
      (@ solution
        (
          vault_parent_id
          vault_mod_hash
          vault_curried_args
          vault_amount
          cat_ann_prefix
          statutes_inner_puzzle_hash
          approved_mod_hashes
          to_issue_amount
          my_coin_id
          target_puzzle_hash
        )
      )
     )

  (include *standard-cl-23*)
  (include condition_codes.clib)
  (include cat_truths.clib)
  (include curry.clib)
  (include statutes_utils.clib)
  (include utils.clib)
  (include vault.clib)

  (if (any (= delta 0) (> 0 delta))
    ; we're issuing or melting
    (assign
      vault_coin_id (calculate-coin-id
        vault_parent_id
        (tree_hash_of_apply vault_mod_hash vault_curried_args)
        vault_amount
      )
      statutes_struct_hash (sha256tree STATUTES_STRUCT)
      statutes_puzzle_hash (calculate-statutes-puzzle-hash STATUTES_STRUCT statutes_inner_puzzle_hash)
      (assert_debug
        (= (strlen cat_ann_prefix) 1)
        (size_b32 vault_curried_args)
        (= vault_mod_hash (f approved_mod_hashes))
        (if (any (all parent_is_cat (> 0 delta)) (all (not parent_is_cat) (= 0 delta)))
          1
          (x "parent_is_cat and delta must be consistent" parent_is_cat delta)
        )
        (list
          (list ASSERT_MY_COIN_ID my_coin_id) ; thanks @bramv-chia
          (list
              ASSERT_COIN_ANNOUNCEMENT
              (sha256
                vault_coin_id
                cat_ann_prefix
                (if (> 0 delta) "x" "i")
                my_coin_id
                (if (> 0 delta) delta to_issue_amount)
                statutes_struct_hash ; need to tie it to statutes struct
              )
          )
          (list REMARK
            (list
              cat_ann_prefix
              (if (> 0 delta) "x" "i")
              vault_coin_id
              (if (> 0 delta) delta to_issue_amount)
              statutes_struct_hash
            )
          )
          (list
            CREATE_COIN_ANNOUNCEMENT
            (concat
              BYC_ANN_PREFIX
              (if (> 0 delta) "x" "i")
              vault_coin_id
              (if (> 0 delta) delta to_issue_amount)
              statutes_struct_hash
            )
          )
          (assert-statute statutes_puzzle_hash STATUTE_TREASURY_COIN_APPROVER_MOD_HASHES approved_mod_hashes)
        )
      )
    )
    (x "no minting")  ; no minting
  )
)
