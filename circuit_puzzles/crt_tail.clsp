; Circuit CAT token (CRT)
(mod (
      GENESIS_ID CAT_MOD_HASH
      Truths parent_is_cat lineage_proof delta inner_conditions
      solution
     ) 
  (include *standard-cl-23*)
  (include condition_codes.clib)
  (include cat_truths.clib)
  (include curry.clib)
  (include utils.clib)
  (include statutes_utils.clib)

  (if delta
    (if (> 0 delta)
      ; melting only by surplus auction
      (assign
        (crt_tail_hash byc_tail_hash surplus_mod_hash
        launcher_id byc_lot_amount last_bid
        statutes_inner_puzzle_hash statutes_struct approved_mod_hashes) solution
        statutes_puzzle_hash (calculate-statutes-puzzle-hash statutes_struct statutes_inner_puzzle_hash)
        puzzle_hash (
          curry_hashes CAT_MOD_HASH
            (sha256 ONE CAT_MOD_HASH)
            (sha256 ONE crt_tail_hash)
            (curry_hashes surplus_mod_hash
              (sha256 ONE surplus_mod_hash)
              (sha256 ONE CAT_MOD_HASH)
              (sha256tree statutes_struct)
              (sha256 ONE launcher_id)
              (sha256 ONE byc_lot_amount)
              (sha256 ONE byc_tail_hash)
              (sha256 ONE crt_tail_hash)
              (sha256tree last_bid)
            )
        )
        (assert
          (size_b32 crt_tail_hash)
          (size_b32 launcher_id)
          (size_b32 byc_tail_hash)
          (size_uint64 byc_lot_amount)
          (size_b32 surplus_mod_hash)
          ; can't melt a non-cat
          parent_is_cat
          ; must be a launched surplus auction
          launcher_id
          ; must have a lot to exchange for melting
          (> byc_lot_amount 0)
          ; should have a winning bid
          (> (f (r last_bid)) 0)
          ; surplus mod hash is the second in approved mod hash list
          (= surplus_mod_hash (f (r approved_mod_hashes)))
          (list
            ; if this is a surplus auction and it called for melting, melt
            (list ASSERT_MY_PUZZLEHASH puzzle_hash)
            ; we should be melting the whole coin value
            (list ASSERT_MY_AMOUNT (* -1 delta))
          )
        )
      )
      ; no minting
      (x 'no minting')
    )
    (if (= (my_parent_cat_truth Truths) GENESIS_ID)
      () ; issuance at launch
      ; third option is issuing by recharge auction or announcer registry
      (assign
        (
          mod_hash
          ann_prefix
          parent_id
          curried_mod_args
          byc_tail_hash
          amount
          issuance_amount
          my_coin_id
          approved_mod_hashes
          statutes_inner_puzzle_hash
          statutes_struct
        ) solution
        statutes_struct_hash (sha256tree statutes_struct)
        statutes_puzzle_hash (calculate-statutes-puzzle-hash statutes_struct statutes_inner_puzzle_hash)
        approving_coin_id (calculate-coin-id
          parent_id
          (if byc_tail_hash
            (curry_hashes CAT_MOD_HASH
              (sha256tree CAT_MOD_HASH)
              (sha256tree byc_tail_hash)
              (tree_hash_of_apply mod_hash curried_mod_args)
            )
            (tree_hash_of_apply mod_hash curried_mod_args)
          )
          amount
        )
        (assert_debug
          ; recharge auction mod is the third in approved mod hash list
          ; can also be announcer registry
          (any
            (= mod_hash (f (r (r approved_mod_hashes)))) ; recharge auction
            (= mod_hash (f (r (r (r (r approved_mod_hashes)))))) ; announcer registry
          )
          (any (size_b32 byc_tail_hash) (not byc_tail_hash))
          (size_uint64 amount)
          (size_uint64 issuance_amount)
          (size_b32 my_coin_id)
          (size_b1 ann_prefix)
          (size_b32 curried_mod_args)
          (size_b32 approving_coin_id)
          (= delta 0)
          (list
            (list ASSERT_MY_COIN_ID my_coin_id)
            (list REMARK approving_coin_id ann_prefix statutes_struct_hash my_coin_id issuance_amount)
            (list CREATE_COIN_ANNOUNCEMENT (concat statutes_struct_hash issuance_amount))
            (list ASSERT_COIN_ANNOUNCEMENT
              (sha256
                approving_coin_id
                ann_prefix
                statutes_struct_hash
                my_coin_id
                issuance_amount
              )
            )
            (list ASSERT_MY_AMOUNT issuance_amount)
            (assert-statute statutes_puzzle_hash STATUTE_TREASURY_COIN_APPROVER_MOD_HASHES approved_mod_hashes)
          )
        )
      )
    )
  )
)
