(mod
  (
    CAT_MOD_HASH
    BYC_TAIL_MOD_HASH
    (@ VAULT_STATE
      (
       COLLATERAL PRINCIPAL AUCTION_STATE INNER_PUZZLE_HASH
       STATUTES_STRUCT DISCOUNTED_PRINCIPAL TRANSFERRED_FEES statutes_puzzle_hash
      )
    )
    (@ args
      (
        tx_current_timestamp
        byc_to_bid
        byc_melting_coin_info
        byc_treasury_coin_info
        liquidation_penalty_percent
        statutes_cumulative_stability_fee_rate
        target_puzzle_hash
        my_coin_id
        auction_timeout
        offer_mod_hash
        treasury_mod_hash
        price_info
        current_stability_fee_rate
      )
    )
  )

  (include *standard-cl-23.1*)
  (include utils.clib)
  (include vault.clib)
  (include statutes_utils.clib)
  (include condition_codes.clib)
  (include curry.clib)

  (assign
    statutes_struct_hash (sha256tree STATUTES_STRUCT)
    BYC_TAIL_HASH (curry_hashes BYC_TAIL_MOD_HASH statutes_struct_hash)
    current_timestamp (+ tx_current_timestamp (* 3 MAX_TX_BLOCK_TIME))
    cumulative_stability_fee_rate (calculate-cumulative-discount-factor
      statutes_cumulative_stability_fee_rate
      current_stability_fee_rate
      current_timestamp
      (r price_info)
    )
    (fees . liquidation_penalty) (calculate-fees
      byc_to_bid
      cumulative_stability_fee_rate
      DISCOUNTED_PRINCIPAL
      PRINCIPAL
      liquidation_penalty_percent
    )
    ; parse auction state as we'll need to verify it's still running and hasn't timed out
    (auction_start_time
     start_price
     step_price_decrease_factor
     step_time_interval
     initiator_puzzle_hash
     initiator_incentive_balance) AUCTION_STATE

    fees_to_pay (if (> TRANSFERRED_FEES fees)
      0
      (- fees TRANSFERRED_FEES)
    )
    fees_to_not_pay (- fees fees_to_pay)
    initiator_incentive (
      if (> initiator_incentive_balance 0)
        (if (> initiator_incentive_balance fees_to_pay)
          fees_to_pay ; all fees go to keeper that initiated the auction
          initiator_incentive_balance ; fees are more than incentive, so we pay the incentive and the rest goes to treasury
        )
        0 ; no incentive
    )
    fees_for_treasury (- fees_to_pay initiator_incentive)
    byc_principal_to_melt (- byc_to_bid fees)
    ; negative because we need negative value in the coin announcements later on
    negative_byc_principal_to_melt (* -1 byc_principal_to_melt)
    undiscounted_principal (* -1 (/ (* -1 DISCOUNTED_PRINCIPAL cumulative_stability_fee_rate) PRECISION))
    discounted_principal (if (= (- byc_to_bid liquidation_penalty) undiscounted_principal)
      0
      (discount-principal-for-melt
        DISCOUNTED_PRINCIPAL
        (* -1 (- byc_to_bid liquidation_penalty))
        cumulative_stability_fee_rate
      )
    )
    ; use all byc provided from keeper to get a piece of collateral
    bid_xch_collateral_amount_pre (/
      (*
        (/
          (* byc_to_bid PRECISION)
          (*
            (- start_price
            (/
              (*
                (* ; current price factor
                  (/
                    (*
                      start_price
                      step_price_decrease_factor
                    )
                    PERCENT_PRECISION
                  )
                  ; current price
                  (/ (- current_timestamp auction_start_time) step_time_interval)
                )
                start_price
              )
              PERCENT_PRECISION)
            )
            10
          )
        )
        MOJOS)
      PRECISION
    )
    bid_xch_collateral_amount (
      if (> bid_xch_collateral_amount_pre COLLATERAL)
        COLLATERAL
        bid_xch_collateral_amount_pre
    )
    leftover_deposit (- COLLATERAL bid_xch_collateral_amount)
    leftover_principal_amount (
      if (> byc_principal_to_melt PRINCIPAL)
        0
        (- PRINCIPAL byc_principal_to_melt)
    )
    byc_coin_id_to_melt (calculate-byc-coin-id
      CAT_MOD_HASH
      BYC_TAIL_HASH
      byc_melting_coin_info
    )
    (treasury_parent treasury_launcher_id treasury_prev_launcher_id treasury_amount) (if byc_treasury_coin_info byc_treasury_coin_info (list 0 0 0 0))
    treasury_coin_id (if byc_treasury_coin_info
      (calculate-byc-coin-id
        CAT_MOD_HASH
        BYC_TAIL_HASH
        (list
          treasury_parent
          treasury_amount
          (curry_hashes
            treasury_mod_hash
            (sha256 ONE treasury_mod_hash)
            (sha256 ONE CAT_MOD_HASH)
            (sha256 ONE BYC_TAIL_HASH)
            statutes_struct_hash
            (sha256 ONE treasury_prev_launcher_id)
            (sha256 ONE treasury_launcher_id)
          )
        )
      )
      ()
    )
    new_treasury_amount (+ fees_for_treasury treasury_amount)
    auction_state (
      ; update auction state based on the bid, which mostly means to set it to null if all debt was paid off
      ; which will push the vault back into usable state for owner of inner puzzle
      if (= leftover_principal_amount 0)
        ()
        (list
          auction_start_time
          start_price
          step_price_decrease_factor
          step_time_interval
          initiator_puzzle_hash
          (- initiator_incentive_balance initiator_incentive)
        )
    )
    (assert
      (> liquidation_penalty_percent 0)
      (> current_timestamp auction_start_time)
      ; needs to be more than zero
      (> byc_principal_to_melt 0)
      (> PRINCIPAL (- byc_principal_to_melt 1))
      ; check that we haven't passed liquidation timeout
      ; is previous auction still running by checking if auction_start_time hasn't timed out
      (> auction_timeout (- current_timestamp auction_start_time))
      ; sanity check for time
      (> current_timestamp (- auction_start_time 1))
      ; check that there is a deposit to bid for
      (> COLLATERAL 0)
      (> leftover_deposit -1)
      (list
        ; auction_state -> (start_time start_price last_auction_price)
        (list
          (if (> TRANSFERRED_FEES fees)
            (- TRANSFERRED_FEES fees)
            0
          )
          leftover_deposit leftover_principal_amount auction_state INNER_PUZZLE_HASH discounted_principal
        )
        (assign
          conditions (list
            (list ASSERT_SECONDS_ABSOLUTE (- tx_current_timestamp MAX_TX_BLOCK_TIME))
            (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ tx_current_timestamp MAX_TX_BLOCK_TIME))
            ; validate coin id
            (list ASSERT_MY_COIN_ID my_coin_id)
            ; signal to tail that it can melt the BYC
            (list SEND_MESSAGE 0x3f
              (concat
                PROTOCOL_PREFIX
                (sha256tree
                  (c 'x'
                    (c (- negative_byc_principal_to_melt fees_to_not_pay)
                      STATUTES_STRUCT
                    )
                  )
                )
              )
              byc_coin_id_to_melt
            )
            ; get a melt confirmation from BYC tail
            ; assert that keeper absorbed their collateral + left over mojos from melting
            (list ASSERT_PUZZLE_ANNOUNCEMENT
              (sha256
                offer_mod_hash
                (sha256tree
                  (c
                    my_coin_id
                    (list
                      (list
                        target_puzzle_hash
                        (+ bid_xch_collateral_amount
                          (+ byc_principal_to_melt
                            ; we already minted these, so return them to keeper
                            fees_to_not_pay
                          )
                        )
                      )
                    )
                  )
                )
              )
            )
            ; announce that we transferred collateral to target puzzle hash
            (if (> initiator_incentive 0)
              ; get an announcement from offer module that BYC was paid to initiator
              (list ASSERT_PUZZLE_ANNOUNCEMENT
                (sha256
                  (calculate-stability-fee-puzzle
                    CAT_MOD_HASH
                    BYC_TAIL_HASH
                    offer_mod_hash
                  )
                  ; nonce = my_coin_id, payments = (initiator puzzle hash, initiator incentive fee)
                  (sha256tree
                    (c my_coin_id
                    (list
                      (list initiator_puzzle_hash initiator_incentive (list initiator_puzzle_hash)))))
                )
              )
              ; else NOOP as we don't need to pay the negative fee
              (list REMARK)
            )
            (assert-statute statutes_puzzle_hash STATUTE_VAULT_AUCTION_PRICE_TTL step_time_interval)
            (assert-statute statutes_puzzle_hash STATUTE_VAULT_AUCTION_PRICE_DECREASE_BPS step_price_decrease_factor)
            (assert-statute statutes_puzzle_hash STATUTE_VAULT_AUCTION_TTL auction_timeout)
            ; assert liquidation penalty percent
            (assert-statute statutes_puzzle_hash STATUTE_VAULT_LIQUIDATION_PENALTY_BPS liquidation_penalty_percent)
            (assert-statute statutes_puzzle_hash STATUTE_TREASURY_MOD_HASH treasury_mod_hash)
            (assert-statute statutes_puzzle_hash STATUTE_OFFER_MOD_HASH offer_mod_hash)
            (assert-price-info statutes_puzzle_hash price_info)
            (assert-statute statutes_puzzle_hash STATUTE_STABILITY_FEE_DF current_stability_fee_rate)
            ; assert cumulative stability fee rate
            (assert-statute statutes_puzzle_hash STATUTE_CUMULATIVE_STABILITY_FEE_DF statutes_cumulative_stability_fee_rate)
          )
          (if (> fees_for_treasury 0)
            ; get an announcement that BYC was paid into treasury
            (c
              (list SEND_MESSAGE 0x3f
                (concat
                  PROTOCOL_PREFIX
                  (sha256tree (c fees_for_treasury new_treasury_amount))
                )
                treasury_coin_id
              )
              conditions
            )
            ; maybe we set fees to zero for some reason, so don't charge anything
            conditions
          )
        )
      )
    )
  )
)