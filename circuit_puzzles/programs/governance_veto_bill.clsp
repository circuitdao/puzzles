(mod
  (
    (BILL MOD_HASH INNER_PUZZLE_HASH CAT_MOD_HASH TAIL_MOD_HASH STATUTES_STRUCT statutes_puzzle_hash)
    (
      amount
      grandparent_veto_id
      veto_amount
      veto_inner_puzzle_hash
      veto_bill_hash
    )
  )

  (include *standard-cl-23*)
  (include condition_codes.clib)
  (include curry.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (defconst ANN_PREFIX 'G')
  (defconst ANN_VETO 'v')
  (defconst ANN_VETOED 'x')

  (defun-inline calculate-crt-puzzle (MOD_HASH CAT_MOD_HASH TAIL_MOD_HASH STATUTES_STRUCT inner_puzzle_hash bill_hash )
    (curry_hashes CAT_MOD_HASH
      (sha256 ONE CAT_MOD_HASH)
      (sha256 ONE TAIL_MOD_HASH)
      (curry_hashes MOD_HASH
        (sha256 ONE MOD_HASH)
        (sha256 ONE CAT_MOD_HASH)
        (sha256 ONE TAIL_MOD_HASH)
        (sha256tree STATUTES_STRUCT)
        (sha256 ONE inner_puzzle_hash)
        bill_hash
      )
    )
  )

  (assign
    (
      (veto_seconds . enact_seconds)
      new_statute_index
      new_statute_value
      new_threshold_amount_to_propose
      new_veto_seconds
      new_delay_seconds
      new_max_delta
    ) BILL
    (assert
      ; can only veto if in proposal mode (BILL is not empty)
      BILL
      (size_b32 veto_inner_puzzle_hash)
      (size_b32 veto_bill_hash)
      (> veto_amount amount)
      (> veto_seconds 0)
      (list
        ()
        (list
          (if (> veto_seconds ONE)
            (list ASSERT_BEFORE_SECONDS_RELATIVE veto_seconds)
            ; noop
            (list ASSERT_SECONDS_RELATIVE veto_seconds)
          )
          (list CREATE_COIN_ANNOUNCEMENT (concat ANN_PREFIX ANN_VETOED))
          ; assert announcement from another CRT coin with higher amount than this one
          (list RECEIVE_MESSAGE
            0x3f
            (concat ANN_PREFIX ANN_VETO (sha256tree BILL))
            (calculate-coin-id
              grandparent_veto_id
              (calculate-crt-puzzle
                MOD_HASH
                CAT_MOD_HASH
                TAIL_MOD_HASH
                STATUTES_STRUCT
                veto_inner_puzzle_hash
                veto_bill_hash
              )
              veto_amount
            )
          )
        )
      )
    )
  )
)