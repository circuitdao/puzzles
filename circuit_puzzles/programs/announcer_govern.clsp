; governance
; we either activate this announcer or deactivate, can happen at any time via CRT vote
(mod ((MOD_HASH STATUTES_STRUCT LAUNCHER_ID INNER_PUZZLE_HASH APPROVED DEPOSIT DELAY ATOM_VALUE COOLDOWN_START LAST_PENALTY_INTERVAL TIMESTAMP_EXPIRES)
      ; solution
      inner_puzzle_hash new_puzzle_hash deposit input_conditions
      (current_timestamp statutes_inner_puzzle_hash input_toggle_activation max_disapproval_penalty_factor)
     )

  (include *standard-cl-23*)
  (include announcer.clib)
  (include condition_codes.clib)
  (include curry.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (assign
    toggle_activation (assert (any (= input_toggle_activation 0) (= input_toggle_activation 1)) input_toggle_activation)
    statutes_puzzle_hash (calculate-statutes-puzzle-hash STATUTES_STRUCT statutes_inner_puzzle_hash)
    min_deposit (- DEPOSIT (/ (/ (* DEPOSIT PRECISION max_disapproval_penalty_factor) PRECISION) PERCENT_PRECISION))
    operation_conditions (recreate-myself-condition MOD_HASH
      STATUTES_STRUCT
      LAUNCHER_ID
      INNER_PUZZLE_HASH
      input_toggle_activation ; current statutes price counter passed
      deposit
      DELAY
      ATOM_VALUE
      COOLDOWN_START
      LAST_PENALTY_INTERVAL
      TIMESTAMP_EXPIRES
      (if (all (= toggle_activation 0) APPROVED)
        ; we're disapproving, use deposit for fees, but can't pay more than the penalty
        (if (all (> DEPOSIT deposit) (> deposit (- min_deposit 1)))
          (c
            (list RESERVE_FEE (- DEPOSIT deposit))
            input_conditions
          )
          ; can't pay more than the penalty
          (x)
        )
        input_conditions
      )
    )
    (li
      (assert-statute statutes_puzzle_hash STATUTE_ANNOUNCER_DISAPPROVAL_MAX_PENALTY_FACTOR max_disapproval_penalty_factor)
      (list ASSERT_SECONDS_ABSOLUTE (- current_timestamp MAX_TX_BLOCK_TIME))
      (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ current_timestamp MAX_TX_BLOCK_TIME))
      (list RECEIVE_MESSAGE 0x10
        (concat
          ANN_PREFIX
          LAUNCHER_ID
          toggle_activation
        )
        statutes_puzzle_hash
      )
      &rest
      operation_conditions
    )
  )
)