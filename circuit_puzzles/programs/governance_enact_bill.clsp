(mod
  (
    (BILL MOD_HASH INNER_PUZZLE_HASH CAT_MOD_HASH CRT_TAIL_HASH STATUTES_STRUCT
     statutes_puzzle_hash
    )
    (
      amount
      parent_parent_id
      prev_bill_hash
      enactment_interval
    )
  )

  (include *standard-cl-23.1*)
  (include curry.clib)
  (include condition_codes.clib)
  (include statutes_utils.clib)
  (include utils.clib)

  (defconst ANN_ENACTED '$')

  (assign
    (
      (veto_seconds . enact_seconds)
      new_statute_index
      new_statute_value
      new_threshold_amount_to_propose
      new_veto_seconds
      new_delay_seconds
      new_max_delta
    ) BILL
    (list
      ; if enactment passes, reset this coin's bill state
      ()
      ; conditions to return
      (list
        ; has enough time passed since this coin was last spent (veto included)
        (list ASSERT_SECONDS_RELATIVE enact_seconds)
        (list SEND_MESSAGE 0x12
          ; we skip the first two elements that are used for voting process, they're not stored in statutes
          (concat PROTOCOL_PREFIX (sha256tree (r BILL)))
          statutes_puzzle_hash
        )
        (list CREATE_COIN_ANNOUNCEMENT (concat PROTOCOL_PREFIX (sha256tree ANN_ENACTED)))
        ; enactment must happen within enactment timeout seconds otherwise times out and resets the bill
        (list ASSERT_BEFORE_SECONDS_RELATIVE (+ enact_seconds enactment_interval))
        (assert-statute statutes_puzzle_hash STATUTE_GOVERNANCE_ENACTMENT_INTERVAL enactment_interval)
      )
    )
  )
)