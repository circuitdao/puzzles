;; Borrow an amount of BYC
;; Issue BYC being borrowed and update PRINCIPAL and other state variables accordingly
(mod
  (
    CAT_MOD_HASH BYC_TAIL_MOD_HASH RUN_TAIL_MOD_HASH
    (@ VAULT_STATE
      (
       COLLATERAL PRINCIPAL AUCTION_STATE INNER_PUZZLE_HASH
       STATUTES_STRUCT DISCOUNTED_PRINCIPAL statutes_puzzle_hash inner_puzzle_hash
      )
    )
    (@ args
      (
        borrow_amount ; amount of byc to be issued to borrower
        minimum_debt_amount
        liquidation_ratio
        price_info
        byc_issuing_coin_info  ; -> (parent_id . amount)
        statutes_cumulative_stability_fee_df
        current_stability_fee_df
        current_timestamp
      )
    )
  )

  (include *standard-cl-23.1*)
  (include utils.clib)
  (include vault.clib)
  (include statutes_utils.clib)
  (include condition_codes.clib)
  (include curry.clib)

  (assign
    ; we want cat puzzle to use the same statutes struct in the tail as this puzzle
    byc_tail_hash (curry_hashes BYC_TAIL_MOD_HASH (sha256tree STATUTES_STRUCT))
    ; calculate the latest cumulative stability fee discount factor
    cumulative_stability_fee_df (calculate-cumulative-discount-factor
      statutes_cumulative_stability_fee_df
      current_stability_fee_df
      current_timestamp
      (r price_info)
    )
    (collateral_price . last_updated) price_info
    new_principal (+ PRINCIPAL borrow_amount)
    ; multiply with negative numbers to ceil
    discounted_borrow_amount (* -1 (/ (* -1 borrow_amount PRECISION) cumulative_stability_fee_df))
    new_discounted_principal (+ DISCOUNTED_PRINCIPAL discounted_borrow_amount)
    ; calculate the coin id for the new BYC coin by enforcing the tail hash
    byc_issuing_coin_id (calculate-byc-coin-id
      CAT_MOD_HASH
      byc_tail_hash
      (list
        (f byc_issuing_coin_info)  ; parent_id
        (r byc_issuing_coin_info)  ; amount
        RUN_TAIL_MOD_HASH
      )
    )
    ; multiply current_stability_fee_df six more times onto cumulative_stability_fee_df
    cumulative_stability_fee_df_adjusted (calculate-cumulative-discount-factor
      cumulative_stability_fee_df
      current_stability_fee_df
      (* 3 MAX_TX_BLOCK_TIME)
      ONE
    )
    new_undiscounted_principal (undiscount-principal new_discounted_principal cumulative_stability_fee_df_adjusted)
    min_collateral_amount (
      get-min-collateral-amount
        new_undiscounted_principal
        (+ liquidation_ratio ONE)
        (f price_info)
    )
    (assert
      (> borrow_amount 0)
      (> new_undiscounted_principal minimum_debt_amount)
      (> COLLATERAL (- min_collateral_amount ONE))
      ; must issue exact borrow amount
      (= (r byc_issuing_coin_info) borrow_amount)
      (list
        ; updated state
        (list COLLATERAL new_principal AUCTION_STATE INNER_PUZZLE_HASH new_discounted_principal)
        ; additional conditions
        (list
          ; signal to tail that it can issue BYC with certain amount
          (list SEND_MESSAGE 0x3f
            (concat PROTOCOL_PREFIX
              (sha256tree (c inner_puzzle_hash (c STATUTES_STRUCT (c "i" borrow_amount))))
            )
            byc_issuing_coin_id
          )
          (assert-statute statutes_puzzle_hash STATUTE_CUMULATIVE_STABILITY_FEE_DF statutes_cumulative_stability_fee_df)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_LIQUIDATION_RATIO_PCT liquidation_ratio)
          (assert-statute statutes_puzzle_hash STATUTE_VAULT_MINIMUM_DEBT minimum_debt_amount)
          (assert-price-info statutes_puzzle_hash price_info)
          ; validate current timestamp is within bounds
          (list ASSERT_SECONDS_ABSOLUTE (- current_timestamp MAX_TX_BLOCK_TIME))
          (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ current_timestamp MAX_TX_BLOCK_TIME))
          (assert-statute statutes_puzzle_hash STATUTE_STABILITY_FEE_DF current_stability_fee_df)
          (assert-statute statutes_puzzle_hash STATUTE_BLOCK_ISSUANCE FALSE)
        )
      )
    )
  )
)
