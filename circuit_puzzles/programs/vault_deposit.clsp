(mod (
  (@ VAULT_STATE
    (
      COLLATERAL PRINCIPAL AUCTION_STATE INNER_PUZZLE_HASH
      STATUTES_STRUCT DISCOUNTED_PRINCIPAL
      statutes_puzzle_hash
    )
  )
  (@ args
    (
      deposit_amount
      current_timestamp ; NEW
      price_info ; NEW
      liquidation_ratio ; NEW
      statutes_cumulative_stability_fee_df ; NEW
      current_stability_fee_df ; NEW
    )
  )
 )

 (include *standard-cl-23.1*)
 (include condition_codes.clib)
 (include utils.clib)
 (include vault.clib)
 (include statutes_utils.clib)


 (assign
   new_collateral (+ COLLATERAL deposit_amount)
   ; NEW START
   cumulative_stability_fee_df (calculate-cumulative-discount-factor
     statutes_cumulative_stability_fee_df
     current_stability_fee_df
     (+ current_timestamp (* 3 MAX_TX_BLOCK_TIME))
     (r price_info)
   )
   undiscounted_principal (undiscount-principal DISCOUNTED_PRINCIPAL cumulative_stability_fee_df)
   min_collateral_amount (get-min-collateral-amount
     undiscounted_principal
     liquidation_ratio
     (f price_info)
   )
   ; NEW END
   (assert
     (> deposit_amount 0)
     (> new_collateral (- min_collateral_amount ONE))  ; NEW. must be at or above liquidation threshold post-deposit
     (list
       (list
         new_collateral
         PRINCIPAL
         AUCTION_STATE
         INNER_PUZZLE_HASH
         DISCOUNTED_PRINCIPAL
       )
       (list
        (list ASSERT_SECONDS_ABSOLUTE (- current_timestamp MAX_TX_BLOCK_TIME)) ; NEW
        (list ASSERT_BEFORE_SECONDS_ABSOLUTE (+ current_timestamp MAX_TX_BLOCK_TIME)) ; NEW
        (assert-price-info statutes_puzzle_hash price_info) ; NEW
        (assert-statute statutes_puzzle_hash STATUTE_VAULT_LIQUIDATION_RATIO_PCT liquidation_ratio) ; NEW
        (assert-statute statutes_puzzle_hash STATUTE_CUMULATIVE_STABILITY_FEE_DF statutes_cumulative_stability_fee_df) ; NEW
        (assert-statute statutes_puzzle_hash STATUTE_STABILITY_FEE_DF current_stability_fee_df) ; NEW
       )
     )
   )
 )

)
